apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.github.dcendents.android-maven'
group = 'com.github.XYOracleNetwork'

/**
 * If set to true, will build with local versions of the dependencies.
 */
def buildWithLocalProjects = true

ext {
    if (!project.hasProperty('local')) {
        local = buildWithLocalProjects
    }
}

subprojects {
    ext {
        local = this.ext.local
    }
}

buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
        google()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$androidPluginVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "com.github.dcendents:android-maven-gradle-plugin:$gradleVersion"
    }
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    if (local) {
        implementation project(':xycore')
    } else {
        implementation 'com.github.XYOracleNetwork:sdk-core-android:0763fe7c4b'
    }

    api "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    api "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.0.1"

    api 'androidx.constraintlayout:constraintlayout:2.0.0-alpha2'
    api "androidx.cardview:cardview:$androidxVersion"
    api "androidx.appcompat:appcompat:$androidxAppcompatVersion"
    api "androidx.browser:browser:$androidxVersion"
    api "androidx.recyclerview:recyclerview:$androidxVersion"

    api 'com.facebook.android:facebook-android-sdk:4.39.0'
    api "me.everything:overscroll-decor-android:1.0.4"
}

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        kotlinOptions {
            allWarningsAsErrors = true
        }
    }

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        defaultPublishConfig 'release'

        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath = true
            }
        }
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            debuggable true
        }
    }
}
